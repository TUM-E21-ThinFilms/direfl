; -- direfl.iss -- an Inno Setup Script for DiRefl

; This script is used by the Inno Setup Compiler to build a Windows XP
; installer/uninstaller for the DANSE Reflectometry application named DiRefl.
; This script was adapted from one generated by the Inno Setup Script Wizard.

; NOTE: In order to support more than one version of the application
; installed simultaneously, the AppName, Desktop shortcut name, and Quick
; Start shortcut name must be unique among versions.  This is in addition to
; having unique names (in the more obvious places) for DefaultDirNam,
; DefaultGroupName, and output file names.  In all, the version string is
; given 8 times in this file, so be sure to do a file wide search/replace
; when updating the version number!

; By default, when installing DiRefl x.y.z:
; - The destination folder will be C:\Program Files\DANSE\DiRefl x.y.z
; - A desktop icon will be created with the label "DiRefl x.y.z"
; - A quickstart icon is optional
; - A start menu folder will be created with the name DANSE -> DiRefl x.y.z
; By default, when uninstalling DiRefl x.y.z
; - The uninstall can be initiated from either the:
;   * Start menu via DANSE -> DiRefl x.y.z -> Uninstall DiRefl
;   * Start menu via Control Panel - > Add or Remove Programs -> DiRefl x.y.z
; - It will not delete the C:\Program Files\DANSE\DiRefl x.y.z folder if it
;   contains any user created files
; - It will delete any desktop or quickstart icons for DiRefl that were
;   created on installation

[Setup]
; Make the AppName string unique so that other versions of the program can be installed simultaniously
AppName=DiRefl 0.4.0
AppVerName=DiRefl 0.4.0
DefaultDirName={pf}\DANSE\DiRefl 0.4.0
DefaultGroupName=DANSE\DiRefl 0.4.0
DisableProgramGroupPage=yes
SourceDir=.
OutputBaseFilename=DiRefl-0.4.0-win32
OutputManifestFile=DiRefl-0.4.0-manifest.txt
OutputDir=.
; Specify an icon file for inclusion in the setup and uninstall programs that will be created
SetupIconFile=direfl.ico
Compression=lzma/max
SolidCompression=yes
PrivilegesRequired=none

; The App* directives are for display in the Add/Remove Programs control panel and are all optional
AppPublisher=University of Maryland
AppPublisherURL=http://www.reflectometry.org/danse/
AppSupportURL=http://www.reflectometry.org/danse/
AppUpdatesURL=http://www.reflectometry.org/danse/

LicenseFile=LICENSE-direfl.txt
;InfoBeforeFile=display_before_install.txt
;InfoAfterFile=display_after_install.txt

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Files]
; This script assumes that the output from the previously run py2exe packaging process is in .\dist\...
; NOTE: Don't use "Flags: ignoreversion" on any shared system files
Source: "dist\*"; DestDir: "{app}"; Flags: ignoreversion recursesubdirs createallsubdirs

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"
Name: "quicklaunchicon"; Description: "{cm:CreateQuickLaunchIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked

[Icons]
Name: "{group}\Launch DiRefl"; Filename: "{app}\direfl.exe"; IconFilename: "{app}\direfl.ico"
Name: "{group}\{cm:ProgramOnTheWeb,DiRefl}"; Filename: "http://www.reflectometry.org/danse/"
Name: "{group}\{cm:UninstallProgram,DiRefl}"; Filename: "{uninstallexe}"
Name: "{commondesktop}\DiRefl 0.4.0"; Filename: "{app}\direfl.exe"; Tasks: desktopicon; WorkingDir: "{app}"; IconFilename: "{app}\direfl.ico"
Name: "{userappdata}\Microsoft\Internet Explorer\Quick Launch\DiRefl 0.4.0"; Filename: "{app}\direfl.exe"; Tasks: quicklaunchicon; WorkingDir: "{app}"; IconFilename: "{app}\direfl.ico"

[Run]
Filename: "{app}\direfl.exe"; Description: "{cm:LaunchProgram,DiRefl}"; Flags: nowait postinstall skipifsilent
Filename: "{app}\README-direfl.txt"; Description: "Read Release Notes"; Verb: "open"; Flags: shellexec waituntilterminated skipifdoesntexist postinstall skipifsilent unchecked
